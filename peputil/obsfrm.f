      SUBROUTINE OBSFRM(TAPIN,TAPOUT,TYPE,IO,IORTRN)
      IMPLICIT NONE

      include 'pepglob.inc'
 
      INTEGER*4 TAPIN,TAPOUT,TYPE,IO,IORTRN
C
C        COMPANION TO OBSIO - J.F.CHANDLER - 1992 JULY
C        USES FORMATTED READS/WRITES OF CARD-IMAGE FILE FOR OBSLIBS.
C        ASSUMES NO ZERO-TRIP LOOP PROBLEMS IN THE INPUT
C
C     OBSLIB TAPES HAVE FOUR DATA TYPES ON THEM
C     TYPES ONE AND TWO ARE TAPE INITIALIZATION AND OCCUR
C     ONLY AT THE START OF A TAPE
C     TYPE THREE INITIATE DATA SERIES
C     TYPE FOUR CONTAIN THE DATA FOR EACH OBSERVATION
C     WITHIN THE SERIES
C     TYPE FOUR RECORDS WITHIN A SERIES OUGHT TO BE TIME SEQUENTIAL
C     BUT THE DATA SERIES THEMSELVES HAVE NO TIME CONSTRAINTS
C
C
C        TAPIN     INPUT TAPE
C        TAPOUT    OUTPUT TAPE
C        TYPE      RECORD TYPE. CAN BE 1,2,3,4
C                  IF 0, THEN REWIND BOTH TAPES
C        IO        READ,WRITE CONTROL
C             +2     WRITE NULL RECORD (TYPE 4 ONLY)
C             +1     WRITE ONLY
C              0     READ AND WRITE
C             -1     READ ONLY (BUT WRITE A NULL TYPE 4 RECORD IF EOF
C                    ENCOUNTERED WITHIN A SERIES, PROVIDED TAPOUT IS
C                    POSITIVE)
C             -2     ABSOLUTELY READ ONLY
C
C        IORTRN    RETURN CODE
C             -3     ILLEGAL TYPE SPECIFIED
C             -2     I/O ERRORS
C             -1     END OF FILE ENCOUNTERED
C              0     END OF TYPE 3 OR 4 RECORDS
C              1     SUCESSFUL COMPLETION
C
C
      CHARACTER*4 OBSIOL/'CPY '/,EXTP,EXTPO/' EXT'/
C
      INCLUDE 't1a.inc'
      INCLUDE 't2a.inc'
      INCLUDE 't3a.inc'
      INCLUDE 't4a.inc'
      INTEGER*2 ICLMPA,IOBSRA,ILMBA(4)
      EQUIVALENCE (ICLMPA,CLAMPA),(IOBSRA,OBSRVA),(ILMBA,LIMBA)
C
C           LOCAL
      INTEGER*2 NSKY1
      INTEGER*4 NEXT1,I,J
      CHARACTER*2 BLNK2
      CHARACTER*4 CZERO4,BLNK4
      CHARACTER*8 BLNK8/'        '/,CZERO8
      INTEGER*4 IZR4(2)/2*0/
      EQUIVALENCE (IZR4,CZERO4,CZERO8),(BLNK2,BLNK4,BLNK8)

C
      IORTRN=1
      IF(TYPE.LE.0) GOTO 50
      GOTO (100,200,300,400),TYPE
C        ILLEGAL TYPE
      IORTRN=-3
      GOTO 990
C
C        REWIND TAPES
   50 IF(TAPIN.GT.0) REWIND TAPIN
      IF(TAPOUT.LE.0) GOTO 990
      ENDFILE TAPOUT
      REWIND TAPOUT
      GOTO 990
C
C
C        TYPE 1
  100 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 150
      READ(TAPIN,101,ERR=910,END=920) R1A
  101 FORMAT(10A8/ A8,A4)
      LNKLVA=OBSIOL
C
  150 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      WRITE(TAPOUT,101) R1A,LNKLVA
      GOTO 990
C
C        TYPE 2
C
  200 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 250
C           CLEAR LDTA VECTOR BEFORE READING
      DO 202 I=1,600
  202 LDTA(I)=0
      READ(TAPIN,203,ERR=910,END=920)
     1 NTAPA,NPRMXA,NPG2A,NCNMXA,ITRTA,JDGS0A,EXTP
  203 FORMAT(I10,2(I4,I6),4I10,A4)
      IF(NPRMXA.GT.NMPRM .OR. NCNMXA.GT.NMBOD) CALL SUICID(
     . 'TOO MANY PARAMETERS ON TYPE 2 RECORD, STOP IN OBSFRM',13)
      IF(NPRMXA.LT.NMPRM) NPRMXA=NMPRM
      IF(NCNMXA.LT.NMBOD) NCNMXA=NMBOD
      IF(EXTP.EQ.EXTPO) THEN
         READ(TAPIN,210,ERR=910,END=920) (PRMA(I),I=1,NPRMXA)
         READ(TAPIN,210,ERR=910,END=920) ((GSCNA(I,J),I=1,NCNMXA),J=1,4)
  210    FORMAT(1P,3D26.19)
         READ(TAPIN,212,ERR=910,END=920) NSITCA
  212    FORMAT(I4)
      ELSE
         READ(TAPIN,211,ERR=910,END=920) (PRMA(I),I=1,NPRMXA)
         READ(TAPIN,211,ERR=910,END=920) ((GSCNA(I,J),I=1,NCNMXA),J=1,4)
  211    FORMAT(1P,3D25.17)
         NSITCA=3
      ENDIF
      READ(TAPIN,215,ERR=910,END=920) (LPRMA(I),I=1,NPRMXA)
  215 FORMAT(16I5)
      READ(TAPIN,216,ERR=910,END=920) ((LGSA(I,J),I=1,NCNMXA),J=1,4)
  216 FORMAT(15I5)
      READ(TAPIN,217,ERR=910,END=920)
     1 JDDT0A,LNKLVA,NUMDTA,NMDT1A,(JDDTA(I),DTA(I),LDTA(I),I=1,NMDT1A)
  217 FORMAT(I10,1X,A4,2I5/ (I10,1P,E17.9,I5))
      READ(TAPIN,220,ERR=910,END=920)
     1 NLABA,(LABA(I),I=1,NLABA)
  220 FORMAT(I5/ (20A4))
      DO I=NPRMXA+1,NMPRM
         PRMA(I)=0._10
         LPRMA(I)=0
      END DO
      DO I=NCNMXA+1,NMBOD
         DO J=1,4
            GSCNA(I,J)=0._10
            LGSA(I,J)=0
         END DO
      END DO
      IF(NUMDTA.LE.0) LDTA(1)=0
      IF(NUMDTA.GT.0) CALL DTCHCK(JDDT0A,NUMDTA,NMDT1A,DTA,LDTA)
      IF(NSITCA.NE.6) NSITCA=3
      NSITCO=NSITCA
C
  250 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      IF(NMDT1A.LE.0) NMDT1A=1
      IF(NLABA.LE.0) NLABA=1
      IF(LABA(1).EQ.CZERO4) LABA(1)=BLNK4
      IF(NSITCA.NE.6) NSITCA=3
      IF(NSITCO.NE.3 .AND. NSITCO.NE.6) NSITCO=NSITCA
      WRITE(TAPOUT,203)
     1 NTAPA,NMPRM,NPG2A,NMBOD,ITRTA,JDGS0A,EXTPO
      WRITE(TAPOUT,210) PRMA
      WRITE(TAPOUT,210) GSCNA
      WRITE(TAPOUT,212) NSITCO
      WRITE(TAPOUT,215) LPRMA
      WRITE(TAPOUT,216) LGSA
      WRITE(TAPOUT,217)
     1 JDDT0A,LNKLVA,NUMDTA,NMDT1A,(JDDTA(I),DTA(I),LDTA(I),I=1,NMDT1A)
      WRITE(TAPOUT,220)
     1 NLABA,(LABA(I),I=1,NLABA)
      GOTO 990
C
C        TYPE 3
C
  300 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 350
      IF(EXTP.EQ.EXTPO) THEN
         READ(TAPIN,301,ERR=910,END=920)
     1    NSEQA,NCODFA,NPLNTA,RITA,SERA,SITA,SPOTA,ERWGTA,ACCTMA,
     2    FDEVA,FREQA,ITIMA,NREWNA,NPG3A,NCENTA
  301    FORMAT(3I5,2(1X,A8,1X,A4),1P,2E17.9/ 2E17.9,D26.19,4I5)
         READ(TAPIN,303,ERR=910,END=920) JDPL0A,(PLCNA(I),I=1,NCNMXA)
  303    FORMAT(I10/1P, (3D26.19))
         READ(TAPIN,216,ERR=910,END=920) (LPLA(I),I=1,NCNMXA)
         READ(TAPIN,303,ERR=910,END=920) JDSB0A,(SBCNA(I),I=1,NCNMXA)
      ELSE
         READ(TAPIN,302,ERR=910,END=920)
     1    NSEQA,NCODFA,NPLNTA,RITA,SERA,SITA,SPOTA,ERWGTA,ACCTMA,
     2    FDEVA,FREQA,ITIMA,NREWNA,NPG3A,NCENTA
  302    FORMAT(3I5,2(1X,A8,1X,A4),1P,2E17.9/ 2E17.9,D25.17,4I5)
         READ(TAPIN,304,ERR=910,END=920) JDPL0A,(PLCNA(I),I=1,NCNMXA)
  304    FORMAT(I10/ (3D25.17))
         READ(TAPIN,216,ERR=910,END=920) (LPLA(I),I=1,NCNMXA)
         READ(TAPIN,304,ERR=910,END=920) JDSB0A,(SBCNA(I),I=1,NCNMXA)
      ENDIF
      READ(TAPIN,216,ERR=910,END=920) (LSBA(I),I=1,NCNMXA)
      IF(NSITCA.EQ.3) THEN
         READ(TAPIN,305,ERR=910,END=920)
     1    PNAMA,((LSCRDA(I,J),I=1,3),J=1,2),NRBSA,RBSA,LRBSA,
     2    NEQNA,EQNA,LEQNA,NCPHA,APHSA,LPHSA,NOBCNA
  305    FORMAT(A8,6I3,I5,1P,2E17.9,2I3/
     1    I5,3E17.9,3I3/ I5,4E17.9/4E17.9/E17.9,9I3,I5)
      ELSE
         READ(TAPIN,1305,ERR=910,END=920)
     1    PNAMA,((LSCRDA(I,J),I=1,6),J=1,2),NRBSA,RBSA,LRBSA,
     2    NEQNA,EQNA,LEQNA,NCPHA,APHSA,LPHSA,NOBCNA
 1305    FORMAT(A8,6I2,1X,6I2,I5,1P,2E17.9,2I3/
     1    I5,3E17.9,3I3/ I5,4E17.9/4E17.9/E17.9,9I3,I5)
      ENDIF
      IF(EXTP.EQ.EXTPO) THEN
         READ(TAPIN,210,ERR=910,END=920) (OBSCNA(I),I=1,NOBCNA)
         READ(TAPIN,210,ERR=910,END=920)
     1    ((SCORDA(I,J),I=1,NSITCA),J=1,2)
      ELSE
         READ(TAPIN,211,ERR=910,END=920) (OBSCNA(I),I=1,NOBCNA)
         READ(TAPIN,211,ERR=910,END=920)
     1    ((SCORDA(I,J),I=1,NSITCA),J=1,2)
      ENDIF
      IF(NSITCA.LT.6) THEN
C FILL IN DEFAULT FOR SITE VELOCITY: T0=J2000, ZERO VELOCITY
         DO J=1,2
            T0SITA(J)=2451545D0
            DO I=NSITCA+1,6
               LSCRDA(I,J)=0
               SCORDA(I,J)=0D0
            END DO
         END DO
      ELSE
         IF(EXTP.EQ.EXTPO) THEN
            READ(TAPIN,210,ERR=910,END=920) T0SITA
         ELSE
            READ(TAPIN,211,ERR=910,END=920) T0SITA
         ENDIF
      ENDIF
      IF(EXTP.EQ.EXTPO) THEN
         READ(TAPIN,308,ERR=910,END=920)
     1    SPCDA,KSITEA,
     2    LSPCDA,NCZNEA,N2A,NCTSSA,N3A,(LCZHRA(I),I=1,N2A)
  308    FORMAT(1P,3D26.19/ 9I5/(40I2))
      ELSE
         READ(TAPIN,309,ERR=910,END=920)
     1    SPCDA,KSITEA,
     2    LSPCDA,NCZNEA,N2A,NCTSSA,N3A,(LCZHRA(I),I=1,N2A)
  309    FORMAT(1P,3D25.17/ 9I5/(40I2))
      ENDIF
      READ(TAPIN,310,ERR=910,END=920)
     1 (LCCHRA(I),LCSHRA(I),I=1,N3A)
  310 FORMAT((16(I3,I2)))
C INSERT ANY NEW LENGTHS HERE FOR COMPATIBILITY
      READ(TAPIN,312,ERR=910,END=920)
     1 NUMTRA,N4A,N5A,N6A,NMPEXA,NAA,NBA
  312 FORMAT(2I5,5I3)
      IF(N5A.EQ.0 .AND. N6A.EQ.0) THEN
         READ(TAPIN,313,ERR=910,END=920)
     1    N5A,N6A,(NTRGA(I),(LTBODA(J,I),J=1,NCNMXA),NTZNEA(I),
     2    (LTZHRA(J,I),J=1,4),NTTSSA(I),(LTCHRA(J,I),J=1,5),
     3    (LTSHRA(J,I),J=1,5),I=1,N4A)
  313    FORMAT(I5,6I2,12I5/12I5/ I5,4I2,I5,10I2)
      ELSE
         DO I=1,N4A
            READ(TAPIN,1313,ERR=910,END=920)
     1       NTRGA(I),(LTBODA(J,I),J=1,NCNMXA)
 1313       FORMAT(I5,6I2,12I5/(15I5))
            READ(TAPIN,2313,ERR=910,END=920) NTZNEA(I),
     1       (LTZHRA(J,I),J=1,4),NTTSSA(I),(LTCHRA(J,I),J=1,5),
     2       (LTSHRA(J,I),J=1,5)
 2313       FORMAT(I5,4I2,I5,10I2)
         END DO
      ENDIF
      DO J=1,N5A
         IF(EXTP.EQ.EXTPO) THEN
            READ(TAPIN,314,ERR=910,END=920)
     1       SCNAMA(J),JDSC0A(J),(LSCA(I,J),SCCNA(I,J),I=1,N6A)
  314       FORMAT(A8,I10/ (2(I5,1PD26.19)))
         ELSE
            READ(TAPIN,315,ERR=910,END=920)
     1       SCNAMA(J),JDSC0A(J),(LSCA(I,J),SCCNA(I,J),I=1,N6A)
  315       FORMAT(A8,I10/ (2(I5,1PD25.17)))
         ENDIF
      END DO
      READ(TAPIN,316,ERR=910,END=920)
     1 NSZNEA,N7A,NSTSSA,N8A,(LSZHRA(I),I=1,N7A)
  316 FORMAT(4I5/ (40I2))
      READ(TAPIN,310,ERR=910,END=920)
     1 (LSCHRA(I),LSSHRA(I),I=1,N8A)
      IF(EXTP.EQ.EXTPO) THEN
         READ(TAPIN,317,ERR=910,END=920)
     1    NPLN2A,SPOT2A,LSPC2A,SPCD2A,FREQ2A,LEMMNA,
     2    NEXTA,((EXNMSA(I,J),I=1,2),J=1,NEXTA)
  317    FORMAT(I5,1X,A4,3I2/ 1P,3D26.19/ D26.19,6I3,I5/ (10A8))
      ELSE
         READ(TAPIN,318,ERR=910,END=920)
     1    NPLN2A,SPOT2A,LSPC2A,SPCD2A,FREQ2A,LEMMNA,
     2    NEXTA,((EXNMSA(I,J),I=1,2),J=1,NEXTA)
  318    FORMAT(I5,1X,A4,3I2/ 1P,3D25.17/ D25.17,6I3,I5/ (10A8))
      ENDIF
      READ(TAPIN,319,ERR=910,END=920)
     1 LNGDA,LNFORA,CTLGA,NSKYCA,(LSKYA(I),I=1,NSKYCA)
  319 FORMAT(2I5,1X,A8,I5/ (40I2))
      READ(TAPIN,320,ERR=910,END=920)
     1 N9A,(LPSRCA(I),I=1,N9A)
  320 FORMAT(I5/ (40I2))
      IF(NAA.GT.0) READ(TAPIN,321,ERR=910,END=920)
     1 (NPLEXA(J),(LPEXA(I,J),I=1,NBA),J=1,NAA)
  321 FORMAT(I5,6I2,12I5/12I5)
      DO I=NCNMXA+1,NMBOD
         PLCNA(I)=0._10
         LPLA(I)=0
         SBCNA(I)=0._10
         LSBA(I)=0
         DO J=1,N4A
            LTBODA(I,J)=0
         END DO
         DO J=1,N5A
            SCCNA(I,J)=0._10
            LSCA(I,J)=0
         END DO
         DO J=1,NAA
            LPEXA(I,J)=0
         END DO
      END DO
      IF(NCODFA.EQ.0) IORTRN=0
      IF(NCODFA.GT.0) CALL FIXSHP(NSZNEA,NSTSSA,LSZHRA,LSCHRA,LSSHRA,
     1 LNFORA,LNGDA,24,N7A,N8A)
      IF(NLABA.LE.1) NEXTA=0
      IF(NSKYCA.LE.1) NSKYCA=0
C
  350 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      IF(NCODFA.EQ.0) THEN
         N2A=0
         N3A=0
         N4A=0
         N5A=0
         N6A=0
         N7A=0
         N8A=0
         N9A=0
         NMPEXA=0
         NOBCNA=0
         NEXTA=0
         NSKYCA=0
      ENDIF
      IF(N2A.LE.0) N2A=1
      IF(N3A.LE.0) N3A=1
      IF(N4A.LE.0) N4A=1
      IF(N5A.LE.0) N5A=1
      IF(N6A.LE.0) N6A=1
      IF(N7A.LE.0) N7A=1
      IF(N8A.LE.0) N8A=1
      IF(N9A.LE.0) N9A=1
      IF(NAA.LE.0 .OR. NBA.LE.1 .OR. NMPEXA.LE.0) THEN
         NAA=1
         NBA=1
         NPLEXA(1)=0
         LPEXA(1,1)=0
      ENDIF
      IF(N6A.LE.1) SCNAMA(1)='        '
      IF(NOBCNA.LE.0) THEN
         OBSCNA(1)=0D0
         NOBCNA=1
      ENDIF
      NEXT1=MAX0(NEXTA,1)
      IF(EXNMSA(1,1).EQ.CZERO8) EXNMSA(1,1)=BLNK8
      IF(EXNMSA(2,1).EQ.CZERO8) EXNMSA(2,1)=BLNK8
      NSKY1=NSKYCA
      IF(NSKYCA.LE.0) NSKY1=1
      WRITE(TAPOUT,301)
     1 NSEQA,NCODFA,NPLNTA,RITA,SERA,SITA,SPOTA,ERWGTA,ACCTMA,
     2 FDEVA,FREQA,ITIMA,NREWNA,NPG3A,NCENTA
      WRITE(TAPOUT,303) JDPL0A,PLCNA
      WRITE(TAPOUT,216) LPLA
      WRITE(TAPOUT,303) JDSB0A,SBCNA
      WRITE(TAPOUT,216) LSBA
      IF(NSITCO.EQ.3) THEN
         WRITE(TAPOUT,305)
     1    PNAMA,((LSCRDA(I,J),I=1,3),J=1,2),NRBSA,RBSA,LRBSA,
     2    NEQNA,EQNA,LEQNA,NCPHA,APHSA,LPHSA,NOBCNA
      ELSE
         WRITE(TAPOUT,1305)
     1    PNAMA,((LSCRDA(I,J),I=1,6),J=1,2),NRBSA,RBSA,LRBSA,
     2    NEQNA,EQNA,LEQNA,NCPHA,APHSA,LPHSA,NOBCNA
      ENDIF
      WRITE(TAPOUT,210) (OBSCNA(I),I=1,NOBCNA)
      WRITE(TAPOUT,210)
     1 ((SCORDA(I,J),I=1,NSITCO),J=1,2)
      IF(NSITCO.GT.3) WRITE(TAPOUT,210) T0SITA
      WRITE(TAPOUT,308)
     1 SPCDA,KSITEA,
     2 LSPCDA,NCZNEA,N2A,NCTSSA,N3A,(LCZHRA(I),I=1,N2A)
      WRITE(TAPOUT,310)
     1 (LCCHRA(I),LCSHRA(I),I=1,N3A)
      WRITE(TAPOUT,312)
     1 NUMTRA,N4A,N5A,N6A,NMPEXA,NAA,NBA
       DO I=1,N4A
          WRITE(TAPOUT,1313)
     1     NTRGA(I),(LTBODA(J,I),J=1,NCNMXA)
          WRITE(TAPOUT,2313) NTZNEA(I),
     1     (LTZHRA(J,I),J=1,4),NTTSSA(I),(LTCHRA(J,I),J=1,5),
     2     (LTSHRA(J,I),J=1,5)
       END DO
      DO J=1,N5A
         WRITE(TAPOUT,314)
     1    SCNAMA(J),JDSC0A(J),(LSCA(I,J),SCCNA(I,J),I=1,N6A)
      END DO
      WRITE(TAPOUT,316)
     1 NSZNEA,N7A,NSTSSA,N8A,(LSZHRA(I),I=1,N7A)
      WRITE(TAPOUT,310)
     1 (LSCHRA(I),LSSHRA(I),I=1,N8A)
      WRITE(TAPOUT,317)
     1 NPLN2A,SPOT2A,LSPC2A,SPCD2A,FREQ2A,LEMMNA,
     2 NEXT1,((EXNMSA(I,J),I=1,2),J=1,NEXT1)
      WRITE(TAPOUT,319)
     1 LNGDA,LNFORA,CTLGA,NSKY1,(LSKYA(I),I=1,NSKY1)
      WRITE(TAPOUT,320)
     1 N9A,(LPSRCA(I),I=1,N9A)
      WRITE(TAPOUT,321)
     1 (NPLEXA(J),(LPEXA(I,J),I=1,NBA),J=1,NAA)
      GOTO 990
C
C        TYPE 4
C
  400 IF(IO.GT.0.OR.TAPIN.LE.0) GOTO 450
      IF(NCODFA.LT.7 .OR. NCODFA.GT.9) THEN
         IF(EXTP.EQ.EXTPO) THEN
            READ(TAPIN,401,ERR=910,END=920)
     1       NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2       ATUTSA,UTUTSA,CLAMPA,LIMBA,OBSRVA,IMNTHA,IDAYA,IYEARA,
     3       JDSA,JDA,CTATA,CTRECA,NSAVA,NUM1A,NUMPRA,NXPA,
     4       (SAVA(I),I=1,NSAVA)
  401       FORMAT(I5,2I3,F20.16,1PD26.19,E17.9/ D26.19,E17.9,D26.19/
     1       E17.9,4A2,3I3,2I10,D26.19/ D26.19,4I5/ (3D26.19))
         ELSE
            READ(TAPIN,402,ERR=910,END=920)
     1       NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2       ATUTSA,UTUTSA,CLAMPA,LIMBA,OBSRVA,IMNTHA,IDAYA,IYEARA,
     3       JDSA,JDA,CTATA,CTRECA,NSAVA,NUM1A,NUMPRA,NXPA,
     4       (SAVA(I),I=1,NSAVA)
  402       FORMAT(I5,2I3,F20.16,1PD25.17,E17.9/ D25.17,E17.9,D25.17/
     1       E17.9,4A2,3I3,2I10,D25.17/ D25.17,4I5/ (3D25.17))
         ENDIF
         DO I=1,4
            IF(ILMBA(I).EQ.0) LIMBA(I)=BLNK2
         END DO
      ELSE
         IF(EXTP.EQ.EXTPO) THEN
            READ(TAPIN,403,ERR=910,END=920)
     1       NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2       ATUTSA,UTUTSA,ICLMPA,LIMBA,IOBSRA,IMNTHA,IDAYA,IYEARA,
     3       JDSA,JDA,CTATA,CTRECA,NSAVA,NUM1A,NUMPRA,NXPA,
     4       (SAVA(I),I=1,NSAVA)
  403       FORMAT(I5,2I3,F20.16,1PD26.19,E17.9/ D26.19,E17.9,D26.19/
     1       E17.9,I2,2A2,I2,3I3,2I10,D26.19/ D26.19,4I5/ 7(3D26.19/),
     2       A8,17X,2D26.19/ 3(3D26.19/), A8,17X,2D26.19/ (3D26.19))
         ELSE
            READ(TAPIN,404,ERR=910,END=920)
     1       NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2       ATUTSA,UTUTSA,ICLMPA,LIMBA,IOBSRA,IMNTHA,IDAYA,IYEARA,
     3       JDSA,JDA,CTATA,CTRECA,NSAVA,NUM1A,NUMPRA,NXPA,
     4       (SAVA(I),I=1,NSAVA)
  404       FORMAT(I5,2I3,F20.16,1PD25.17,E17.9/ D25.17,E17.9,D25.17/
     1       E17.9,I2,2A2,I2,3I3,2I10,D25.17/ D25.17,4I5/ 7(3D25.17/),
     2       A8,17X,2D25.17/ 3(3D25.17/), A8,17X,2D25.17/ (3D25.17))
         ENDIF
         DO I=1,4
            IF(ILMBA(I).EQ.0) LIMBA(I)=BLNK2
         END DO
      ENDIF
      DO J=1,NUM1A
         IF(EXTP.EQ.EXTPO) THEN
            READ(TAPIN,405,ERR=910,END=920) (DERIVA(I,J),I=1,NUMPRA)
  405       FORMAT(1P,3D26.19)
            READ(TAPIN,405,ERR=910,END=920) (DVXA(I,J),I=1,NXPA)
         ELSE
            READ(TAPIN,406,ERR=910,END=920) (DERIVA(I,J),I=1,NUMPRA)
  406       FORMAT(1P,3D25.17)
            READ(TAPIN,406,ERR=910,END=920) (DVXA(I,J),I=1,NXPA)
         ENDIF
      END DO
      READ(TAPIN,407,ERR=910,END=920)
     1 ILDT1A,ILDT2A,ILDT3A,ILDT4A,ILDT5A,ILDT6A,
     2 NCALA,SUMCRA,(CALA(I),SCALA(I),ICALA(I),I=1,NCALA)
  407 FORMAT(7I5,1P,2E17.9/ (2E17.9,I5,2E17.9,I5))
      READ(TAPIN,408,ERR=910,END=920)
     1 LNSHBA,LXSHPA,(LSHOBA(I),I=1,LNSHBA)
  408 FORMAT(16I5)
      IF(NUMDTA.GT.0) CALL DTCKI(ILDT1A)
      IF(NCODEA.EQ.0) IORTRN=0
C
  450 IF(IO.LT.0.OR.TAPOUT.LE.0) GOTO 990
      IF(IO.GT.1) GOTO 930
      IF(NCODEA.EQ.0) THEN
         NSAVA=0
         NUM1A=0
         NUMPRA=0
         NCALA=0
         NXPA=0
      ENDIF
      IF(NSAVA.LE.0) SAVA(1)=0D0
      IF(NSAVA.LE.0) NSAVA=1
      IF(NUM1A.LE.0) NUM1A=1
      IF(NUMPRA.LE.0) NUMPRA=1
      IF(NXPA.LE.0) NXPA=1
      IF(NCALA.LE.0) NCALA=1
      IF(LNSHBA.LE.0) LNSHBA=1
  460 IF(NCODFA.LT.7 .OR. NCODFA.GT.9) THEN
         DO I=1,4
            IF(ILMBA(I).EQ.0) LIMBA(I)=BLNK2
         END DO
         WRITE(TAPOUT,401)
     1   NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2   ATUTSA,UTUTSA,CLAMPA,LIMBA,OBSRVA,IMNTHA,IDAYA,IYEARA,JDSA,JDA,
     3   CTATA,CTRECA,NSAVA,NUM1A,NUMPRA,NXPA,(SAVA(I),I=1,NSAVA)
      ELSE
         DO I=1,2
            IF(ILMBA(I).EQ.0) LIMBA(I)=BLNK2
         END DO
         IF(ICLMPA.LT.-9 .OR. ICLMPA.GT.99) ICLMPA=0
         WRITE(TAPOUT,403)
     1   NCODEA,IHRA,IMINA,SECA,(RESLTA(I),ERRORA(I),I=1,2),
     2   ATUTSA,UTUTSA,ICLMPA,LIMBA,IOBSRA,IMNTHA,IDAYA,IYEARA,JDSA,JDA,
     3   CTATA,CTRECA,NSAVA,NUM1A,NUMPRA,NXPA,(SAVA(I),I=1,NSAVA)
      ENDIF
      DO 463 J=1,NUM1A
      WRITE(TAPOUT,405) (DERIVA(I,J),I=1,NUMPRA)
      WRITE(TAPOUT,405) (DVXA(I,J),I=1,NXPA)
  463 CONTINUE
      WRITE(TAPOUT,407)
     1 ILDT1A,ILDT2A,ILDT3A,ILDT4A,ILDT5A,ILDT6A,
     2 NCALA,SUMCRA,(CALA(I),SCALA(I),ICALA(I),I=1,NCALA)
      WRITE(TAPOUT,408)
     1 LNSHBA,LXSHPA,(LSHOBA(I),I=1,LNSHBA)
      GOTO 990
C
C        READ ERROR, SET RETURN CODE
  910 IORTRN=-2
      GOTO 990
C
C        END OF FILE ENCOUNTERED, SET CODE
  920 IORTRN=-1
C
C        IF T4 RECORD AND ALSO WRITING, WRITE OUT RECORD WITH NCODE=0
      IF(TYPE.NE.4.OR.IO.LT.-1) GOTO 990
  930 NCODEA=0
      NSAVA=1
      NUM1A=1
      NUMPRA=1
      NXPA=1
      NCALA=1
      LNSHBA=1
      IF(TAPOUT.GT.0) GOTO 460
C
C
990   RETURN
      END
