C|CTCHAR
      IMPLICIT NONE
C           J.F.CHANDLER - 1987 MAY - PROGRAM CTCHAR
C           CONVERT CT-AT FROM BINARY TO CHARACTER REPRESENTATION
C           OR VICE VERSA
C           MOD 2001 JUL TO SATISFY LAHEY FORTRAN
C
C     CONTROLS: NAMELIST 'INPUT' ON FORTRAN UNIT 5
C           INTA    TABULAR INTERVAL IN DAYS (DEF=4)
C           JD(2)   START AND STOP DATES (TO OVERRIDE AVAILABLE TIMES)
C           JEPH    INPUT FORTRAN UNIT (1, 2, OR 3)
C           NEWEPH  OUTPUT FORTRAN UNIT (DEF=4 UNLESS JEPH2)
C           NPLX    PLANET NUMBER OF CT-AT INTEGRATION (DEF=3)
C           NPRMAX  MAXIMUM POINTS PER 'RECORD' ON OUTPUT. DEF=27
C           PRMTCT  VALUES FOR PRMTER ARRAY IF WRITING UNIT 4
C
C     INPUT PULSAR EPHEMERIS:  FORTRAN UNIT 1, RECFM=F(B),LRECL=132
C  OR       CT-AT INTEGRATION: FORTRAN UNIT 2, RECFM=V(BS),LRECL=900
C  OR       CT-AT INTEGRATION: FORTRAN UNIT 3, RECFM=F(B),LRECL=80
C
C     OUTPUT CT-AT INTEGRATION: FORTRAN UNIT 3, RECFM=F(B),LRECL=80
C                                               (UNLESS INPUT WAS 3)
C        PLUS OPTIONAL UNIT 4 IN THE FORM OF 2 IF INPUT WAS 1 OR 3
C
      REAL*10 CTATVL(200)
      REAL*10 AULTSC,CTMFCT,DOT,EMRAT,OBLQ,VELC
      INTEGER*4 I,INTA,JD(2),JD1,JD2,JDI,JDNXT,JDO,JDP,JEPH,MM,NEWEPH,
     . NN,NPLX,NPRI,NPRMAX,NPRO
C           TEMPORARY STORAGE
      REAL*10 PRMTCT(100),MASS9(9)
      REAL*8 ZERO/0D0/
      EQUIVALENCE (MASS9(1),CTATVL(1),PRMTCT(1)),(TITLE(1),CTATVL(101))
      CHARACTER*8 CTTIT(16)/'CT - AT ','- 32.15 ','SEC',8*' ',
     1 ' G.R. PR','OPER TIM','E INTEGR','ATION W/','MONTHLY.'/,TITLE(16)
      CHARACTER*4 WORD, MASWRD/'MASS'/, PRMWRD/'PRM='/
      CHARACTER*2 FTYPE, XTND/'DP'/
      REAL*10 EMBC(6),XMOON(6)
      LOGICAL*4 FIRST
C
      NAMELIST/INPUT/ JD,NPRMAX,JEPH,INTA,NPLX,NEWEPH,PRMTCT
C
      INTA=4
      JD(1)=0
      JD(2)=99999999
      JEPH=0
      NEWEPH=4
      NPLX=3
      NPRI=1
      NPRMAX=0
      CALL ZFILL(PRMTCT,16*100)
      DO 40 I=1,16
   40 TITLE(I)=CTTIT(I)
      READ(5,INPUT,END=50)
   50 CONTINUE
      IF(NPRMAX.EQ.0.AND.JEPH.EQ.3) NPRMAX=100
      IF(NPRMAX.EQ.0) NPRMAX=27
      IF(JEPH.NE.1 .AND. JEPH.NE.3) JEPH=2
      IF(JEPH.EQ.2 .OR. NEWEPH.EQ.JEPH) NEWEPH=0
C
C           COPY HEADERS
      IF(JEPH.EQ.1) THEN
         READ(JEPH,60) (TITLE(I),I=6,9)
      ELSE IF(JEPH.EQ.2) THEN
         READ(JEPH) TITLE
      ELSE
         READ(JEPH,80) TITLE,FTYPE
      ENDIF
   60 FORMAT(104X,3A8,A4)
      IF(JEPH.NE.3) WRITE(3,80) TITLE,XTND
   80 FORMAT(10A8/6A8,A2)
      IF(NEWEPH.GT.0) WRITE(NEWEPH) TITLE
C
      IF(JEPH.EQ.1) THEN
         READ(JEPH,85) JD1,JD2,AULTSC,VELC,EMRAT,OBLQ
   85    FORMAT(2I8/6(5X,F16.0))
         CTMFCT=AULTSC**2/864D2/EMRAT
         PRMTCT(10)=1D0/EMRAT
         PRMTCT(50)=AULTSC
         PRMTCT(100)=VELC
      ELSE IF(JEPH.EQ.2) THEN
         READ(JEPH) JD1,JD2,INTA,NPLX,PRMTCT
      ELSE
         READ(JEPH,88) JD1,JD2,INTA,NPLX,WORD
   88    FORMAT(3X,2I8,5X,I3,5X,I3,9X,A4)
         IF(WORD.EQ.MASWRD) READ(JEPH,89) MASS9
   89    FORMAT(1P,3D23.15)
         IF(WORD.EQ.PRMWRD) READ(JEPH,89) PRMTCT
      ENDIF
      JDNXT=JD1
      JD(1)=(JD(1)-1)/INTA*INTA+1
      IF(JD(1).GT.JD1) JD1=JD(1)
      JD(2)=(JD(2)+INTA-1)/INTA*INTA+1
      IF(JD(2).LT.JD2) JD2=JD(2)
      IF(NPRMAX.LT.11) NPRMAX=11
      IF(NPRMAX.GT.100) NPRMAX=100
      IF(JEPH.NE.3) WRITE(3,90) JD1,JD2,INTA,NPLX,NPRMAX,MASS9
   90 FORMAT('JD=',2I8,' INT=',I3,' NPL=',I3,' MAX=',I3,' MASS='/
     1 (1P,3D23.15))
      IF(NEWEPH.GT.0) WRITE(NEWEPH) JD1,JD2,INTA,NPLX,PRMTCT,
     1 (ZERO,I=1,10)
C
      NPRO=NPRMAX
      NN=0
      JDO=JD1
      FIRST=.TRUE.
C
C           READ A RECORD
  100 IF(JEPH.EQ.1) THEN
         READ(JEPH,115) JDI,CTATVL(NN+1),EMBC,XMOON
  115    FORMAT(I8/ 3P,F11.9,0P,6F20.17/ 11X,6F20.17/////////)
         JDI=JDI+1
         CTATVL(NN+1)=CTATVL(NN+1)-DOT(EMBC(4),XMOON)*CTMFCT
      ELSE IF(JEPH.EQ.2) THEN
         READ(JEPH) JDI,NPRI,(CTATVL(I+NN),I=1,NPRI)
      ELSE
         IF(FTYPE.EQ.XTND) THEN
            READ(JEPH,117) JDI,NPRI,(CTATVL(I+NN),I=1,NPRI)
  117       FORMAT(I8,2X,I3,7X,3P,3F20.17/(4F20.17))
         ELSE
            READ(JEPH,116) JDI,NPRI,(CTATVL(I+NN),I=1,NPRI)
  116       FORMAT(I8,2X,I3,3P,5F13.9/(6F13.9))
         ENDIF
         JDI=JDI+1
      ENDIF
  120 IF(JDI.EQ.JDNXT) GOTO 140
      WRITE(6,130) JDNXT,JDI
  130 FORMAT(' ***EXPECTED JD=',I8,' INPUT HAD JD=',I8,' ***')
      STOP 4
C
  140 JDNXT=JDNXT+NPRI*INTA
      IF(JDNXT.LE.JD1) GOTO 100
      IF(FIRST) MM=(JDO-JDI)/INTA
      FIRST=.FALSE.
      NN=NN+NPRI
C
C           WRITE ONE RECORD
  150 IF(MM+NPRO.GT.NN) GOTO 200
      JDP=JDO-1
      IF(JEPH.NE.3) WRITE(3,175) JDP,NPRO,(CTATVL(I+MM),I=1,NPRO)
  175 FORMAT(I8,'.5',I3,7X,3P,3F20.17/(4F20.17))
      IF(NEWEPH.GT.0) WRITE(NEWEPH) JDO,NPRO,(CTATVL(I+MM),I=1,NPRO),
     1 (ZERO,I=1,10)
      MM=MM+NPRO
      JDO=JDO+INTA*NPRO
      IF(JDO.GE.JD2) GOTO 400
      GOTO 150
C
C           SHIFT DATA TO MAKE ROOM FOR ANOTHER RECORD
  200 IF(JDNXT.GE.JD2) GOTO 300
      NN=NN-MM
      IF(NN.LE.0 .OR. MM.EQ.0) GOTO 220
      DO 210 I=1,NN
  210 CTATVL(I)=CTATVL(I+MM)
  220 MM=0
      GOTO 100
C
C           WRITE ONE LAST RECORD, IF NECESSARY
  300 NPRO=NN-MM
      IF(NPRO.GT.0) GOTO 150
C
C           ALL DONE
  400 REWIND JEPH
      IF(JEPH.NE.3) REWIND 3
      IF(NEWEPH.GT.0) REWIND NEWEPH
      STOP
      END
